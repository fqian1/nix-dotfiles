{
  pkgs,
  lib,
  ...
}: let
  colorSchemes = {
    occult = {
      base00 = "#0A0A12";
      base01 = "#14141E";
      base02 = "#1C1C28";
      base03 = "#2A2A38";
      base04 = "#3A3A4A";
      base05 = "#E4DED2";
      base06 = "#F2EADF";
      base07 = "#FFF8F2";
      base08 = "#C25B5B";
      base09 = "#E08A66";
      base0A = "#E6C27A";
      base0B = "#8BAA82";
      base0C = "#95B3B0";
      base0D = "#6270A8";
      base0E = "#9A7398";
      base0F = "#B86C6A";
    };
    default_dark = {
      base00 = "#181818";
      base01 = "#282828";
      base02 = "#383838";
      base03 = "#585858";
      base04 = "#b8b8b8";
      base05 = "#d8d8d8";
      base06 = "#e8e8e8";
      base07 = "#f8f8f8";
      base08 = "#ab4642";
      base09 = "#dc9656";
      base0A = "#f7ca88";
      base0B = "#a1b56c";
      base0C = "#86c1b9";
      base0D = "#7cafc2";
      base0E = "#ba8baf";
      base0F = "#a16946";
    };
  };

  schemesDir = pkgs.linkFarm "color-schemes" (
    pkgs.lib.mapAttrsToList (name: value: {
      name = "${name}.json";
      path = pkgs.writeText "${name}.json" (builtins.toJSON value);
    })
    colorSchemes
  );

  themeStateFile = "$HOME/.cache/shell-theme.sh";
in {
  home.sessionVariables = {
    COLOR_SCHEMES_DIR = schemesDir;
  };

  programs.bash.initExtra = lib.mkAfter ''
    if [ -f "${themeStateFile}" ]; then
      source "${themeStateFile}"
    fi
  '';

  home.packages = [
    (pkgs.writeShellApplication {
      name = "change-scheme";
      runtimeInputs = [pkgs.fzy pkgs.jq pkgs.findutils pkgs.coreutils];
      excludeShellChecks = ["SC2034" "SC2059" "SC1090" "SC2016" "SC2028"];
      text = ''
        SCHEMES_DIR=${schemesDir}
        THEME_FILE="${themeStateFile}"

        THEME=$(find "$SCHEMES_DIR" -name "*.json" -printf "%f\n" | sed 's/\.json$//' | fzy)

        [[ -z "$THEME" ]] && exit 0

        mkdir -p "$(dirname "$THEME_FILE")"

        read -r -a B <<< "$(jq -r '[.base00, .base01, .base02, .base03, .base04, .base05, .base06, .base07, .base08, .base09, .base0A, .base0B, .base0C, .base0D, .base0E, .base0F] | @sh' "$SCHEMES_DIR/$THEME.json" | tr -d "'")"

        {
          echo "# Generated by change-scheme"

          echo 'put_template() { printf "\033]%d;%s\007" "$1" "$2"; }'

          # Background / Foreground / Cursor
          echo "put_template 11 \"''${B[0]}\""
          echo "put_template 10 \"''${B[5]}\""

          # ANSI Colors
          echo "put_template 4 \"0;''${B[1]}\""
          echo "put_template 4 \"1;''${B[8]}\""
          echo "put_template 4 \"2;''${B[11]}\""
          echo "put_template 4 \"3;''${B[10]}\""
          echo "put_template 4 \"4;''${B[13]}\""
          echo "put_template 4 \"5;''${B[14]}\""
          echo "put_template 4 \"6;''${B[12]}\""
          echo "put_template 4 \"7;''${B[4]}\""
          echo "put_template 4 \"8;''${B[3]}\""
          echo "put_template 4 \"9;''${B[8]}\""
          echo "put_template 4 \"10;''${B[11]}\""
          echo "put_template 4 \"11;''${B[9]}\""
          echo "put_template 4 \"12;''${B[13]}\""
          echo "put_template 4 \"13;''${B[15]}\""
          echo "put_template 4 \"14;''${B[2]}\""
          echo "put_template 4 \"15;''${B[6]}\""

          echo 'unset -f put_template'

        } > "$THEME_FILE"

        source "$THEME_FILE"

        echo "Applied: $THEME"
      '';
    })
  ];
}
# Logic used to store base16 palette while maintaining similar ansi 15 palette:
# Ansi 0:  Black          -> base01 : Bright Black!  (Lighter Background - Status Bar, Line Highlight)
# Ansi 1:  Red            -> base08 : Red            (Variables, Errors, Git Delete)
# Ansi 2:  Green          -> base0B : Green          (Strings, Success, Git Add)
# Ansi 3:  Yellow         -> base0A : Yellow         (Types, Classes, Search HL, Warn)
# Ansi 4:  Blue           -> base0D : Blue           (Functions, Methods, Ids)
# Ansi 5:  Magenta        -> base0E : Magenta        (Control Flow, Keywords)
# Ansi 6:  Cyan           -> base0C : Cyan           (Regex, Escape Chars, Support)
# Ansi 7:  White          -> base04 : Medium Grey!   (Subtle Foreground - Unimportant Text, Status Bar FG)
# Ansi 8:  Bright Black   -> base03 : Grey!          (Faint Foreground - Comments, Invisible Characters, Line Numbers)
# Ansi 9:  Bright Red     -> base08 : Red            (Duplicate)
# Ansi 10: Bright Green   -> base0B : Green          (Duplicate)
# Ansi 11: Bright Yellow  -> base09 : Orange!        (Numbers, Bools, Consts, Warn)
# Ansi 12: Bright Blue    -> base0D : Blue           (Duplicate)
# Ansi 13: Bright Magenta -> base0F : Brown/Maroon!  (Deprecated Code, Headers, Embedded code)
# Ansi 14: Bright Cyan    -> base02 : Dark Grey!     (Selection Background)
# Ansi 15: Bright White   -> base06 : Off-White      (Light Foreground - High Contrast, Bright FG)
# Default Background      -> base00 : Black          (Background)
# Default Foreground      -> base05 : Light Grey     (Foreground)
# Base07 Omitted - Pure white seldom used
