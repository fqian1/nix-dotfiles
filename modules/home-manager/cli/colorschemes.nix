{
  pkgs,
  lib,
  ...
}: let
  colorSchemes = {
    occult = {
      base00 = "#0A0A12";
      base01 = "#14141E";
      base02 = "#1C1C28";
      base03 = "#2A2A38";
      base04 = "#3A3A4A";
      base05 = "#E4DED2";
      base06 = "#F2EADF";
      base07 = "#FFF8F2";
      base08 = "#C25B5B";
      base09 = "#E08A66";
      base0A = "#E6C27A";
      base0B = "#8BAA82";
      base0C = "#95B3B0";
      base0D = "#6270A8";
      base0E = "#9A7398";
      base0F = "#B86C6A";
    };
    default_dark = {
      base00 = "#181818";
      base01 = "#282828";
      base02 = "#383838";
      base03 = "#585858";
      base04 = "#b8b8b8";
      base05 = "#d8d8d8";
      base06 = "#e8e8e8";
      base07 = "#f8f8f8";
      base08 = "#ab4642";
      base09 = "#dc9656";
      base0A = "#f7ca88";
      base0B = "#a1b56c";
      base0C = "#86c1b9";
      base0D = "#7cafc2";
      base0E = "#ba8baf";
      base0F = "#a16946";
    };
  };

  schemesDir = pkgs.linkFarm "color-schemes" (
    pkgs.lib.mapAttrsToList (name: value: {
      name = "${name}.json";
      path = pkgs.writeText "${name}.json" (builtins.toJSON value);
    })
    colorSchemes
  );

  themeStateFile = "$HOME/.cache/shell-theme.sh";
in {
  home.sessionVariables = {
    COLOR_SCHEMES_DIR = schemesDir;
  };

  programs.bash.initExtra = lib.mkAfter ''
    if [ -f "${themeStateFile}" ]; then
      source "${themeStateFile}"
    fi
  '';

  home.packages = [
    (pkgs.writeShellApplication {
      name = "change-scheme";
      runtimeInputs = [pkgs.fzy pkgs.jq pkgs.findutils pkgs.coreutils];
      excludeShellChecks = ["SC2034" "SC2059" "SC1090" "SC2016" "SC2028"];
      text = ''
        SCHEMES_DIR=${schemesDir}
        THEME_FILE="${themeStateFile}"

        THEME=$(find "$SCHEMES_DIR" -name "*.json" -printf "%f\n" | sed 's/\.json$//' | fzy)

        [[ -z "$THEME" ]] && exit 0

        mkdir -p "$(dirname "$THEME_FILE")"

        read -r -a B <<< "$(jq -r '[.base00, .base01, .base02, .base03, .base04, .base05, .base06, .base07, .base08, .base09, .base0A, .base0B, .base0C, .base0D, .base0E, .base0F] | @sh' "$SCHEMES_DIR/$THEME.json" | tr -d "'")"

        {
          echo "# Generated by change-scheme"

          echo 'put_template() { printf "\033]%d;%s\007" "$1" "$2"; }'

          # Background / Foreground / Cursor
          echo "put_template 11 \"''${B[0]}\""
          echo "put_template 10 \"''${B[5]}\""

          # ANSI Colors
          echo "put_template 4 \"0;''${B[1]}\""
          echo "put_template 4 \"1;''${B[8]}\""
          echo "put_template 4 \"2;''${B[11]}\""
          echo "put_template 4 \"3;''${B[10]}\""
          echo "put_template 4 \"4;''${B[13]}\""
          echo "put_template 4 \"5;''${B[14]}\""
          echo "put_template 4 \"6;''${B[12]}\""
          echo "put_template 4 \"7;''${B[4]}\""
          echo "put_template 4 \"8;''${B[3]}\""
          echo "put_template 4 \"9;''${B[8]}\""
          echo "put_template 4 \"10;''${B[11]}\""
          echo "put_template 4 \"11;''${B[9]}\""
          echo "put_template 4 \"12;''${B[2]}\""
          echo "put_template 4 \"13;''${B[15]}\""
          echo "put_template 4 \"14;''${B[12]}\""
          echo "put_template 4 \"15;''${B[6]}\""

          echo 'unset -f put_template'

        } > "$THEME_FILE"

        source "$THEME_FILE"

        echo "Applied: $THEME"
      '';
    })
  ];
}
# Logic used to store base16 palette while maintaining similar ansi 15 palette:

# Default Background :     -> 0  (Base00): Black       (Background)
# Black          : Ansi 0  -> 1  (Base01): BrightBlack (Status bars, UI panels)
# Red            : Ansi 1  -> 8  (Base08): Red         (Variables, XML Tags, Markup Error)
# Green          : Ansi 2  -> 11 (Base0B): Green       (Strings, Inherited Class, Markup Added)
# Yellow         : Ansi 3  -> 10 (Base0A): Yellow      (Classes, Bold, Markup Warnings)
# Blue           : Ansi 4  -> 13 (Base0D): Blue        (Functions, Methods, Attribute IDs)
# Magenta        : Ansi 5  -> 14 (Base0E): Magenta     (Keywords, Storage, Selector Tag)
# Cyan           : Ansi 6  -> 12 (Base0C): Cyan        (Support, Regex, Escape Characters)
# White          : Ansi 7  -> 4  (Base04): LightGrey   (Subtle text)
# Bright Black   : Ansi 8  -> 3  (Base03): Grey        (Invisible characters)
# Bright Red     : Ansi 9  -> 8  (Base08): Red         (Variables, XML Tags, Markup Error)
# Bright Green   : Ansi 10 -> 11 (Base0B): Green       (Strings, Inherited Class, Markup Added)
# Bright Yellow  : Ansi 11 -> 9  (Base09): Orange      (Integers, Boolean, Constants)
# Bright Blue    : Ansi 12 -> 2  (Base02): DarkGrey    (Highlighted lines)
# Bright Magenta : Ansi 13 -> 15 (Base0F): Brown       (Deprecated, Opening/Closing Embedded Tags)
# Bright Cyan    : Ansi 14 -> 12 (Base0C): Cyan        (Support, Regex, Escape Characters)
# Bright White   : Ansi 15 -> 6  (Base06): White       (Emphasis)
# Default Foreground :     -> 5  (Base05): OffWhite    (Primary text, Variables)

# Base07 Omitted - Pure white seldom used
