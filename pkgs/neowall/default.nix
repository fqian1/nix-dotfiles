{
  lib,
  stdenv,
  fetchFromGitHub,
  pkg-config,
  wayland,
  mesa,
  libpng,
  libjpeg,
  libglvnd,
  wayland-protocols,
}:

stdenv.mkDerivation rec {
  pname = "neowall";
  version = "0.4.1";

  src = fetchFromGitHub {
    owner = "1ay1";
    repo = "neowall";
    rev = "83c5c923e4dcc31a405a3480c63fec62203c7ada";
    sha256 = "sha256-Ybms3++7ql5X/xxfUywp6ClxA7rKlzHYfb13yhORR7E=";
    # sha256 = lib.fakeSha256;
  };

  nativeBuildInputs = [
    pkg-config
  ];

  buildInputs = [
    wayland
    mesa
    libglvnd
    libpng
    libjpeg
    wayland-protocols
  ];

  # THE FIX: (as of 83c5c923e4dcc31a405a3480c63fec62203c7ada)
  # The upstream code calls X11 functions even when X11 is disabled.
  # We create a dummy C file in the src directory. The Makefile's wildcard
  # rule ($(wildcard $(SRC_DIR)/*.c)) will automatically pick this up
  # and link it, satisfying the undefined references.
  postPatch = ''
    cat > src/x11_stubs.c <<EOF
    /* Stubs generated by Nix to fix pure Wayland build */
    int x11_backend_get_fd() { return -1; }
    void x11_backend_handle_events() { }
    int compositor_backend_x11_init() { return -1; }
    EOF
  '';

  installPhase = ''
    runHook preInstall
    install -Dm755 build/bin/neowall $out/bin/neowall
    runHook postInstall
  '';

  meta = with lib; {
    description = "ğŸ¨ A reliable Wayland wallpaper engine written in C. Multi-monitor support, smooth transitions, hot-reload. For Sway, Hyprland, River.";
    homepage = "https://github.com/1ay1/neowall";
    license = licenses.mit;
    platforms = platforms.linux;
    maintainers = [ ];
  };
}
